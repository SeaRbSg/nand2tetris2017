/**
 * Computes the sum of three bits.
 */

CHIP FullAdder {
    IN a, b, c;  // 1-bit inputs
    OUT sum,     // Right bit of a + b + c
        carry;   // Left bit of a + b + c

    PARTS:
    Nand(a=b, b=c, out=bcn);
    Nand(a=b, b=bcn, out=bn);
    Nand(a=c, b=bcn, out=cn);
    Nand(a=bn, b=cn, out=s);
    Nand(a=a, b=s, out=asn);
    Nand(a=a, b=asn, out=an);
    Nand(a=s, b=asn, out=sn);
    Nand(a=an, b=sn, out=sum);
    Nand(a=bcn, b=asn, out=carry);
}

/**
 *     a  b  c  carry sum
 *     0  0  0    0    0
 *     0  0  1    0    1
 *     0  1  0    0    1
 *     0  1  1    1    0
 *     1  0  0    0    1
 *     1  0  1    1    0
 *     1  1  0    1    0
 *     1  1  1    1    1
 *
 *          carry                    sum
 *     c\ab 00 01 11 10        c\ab 00 01 11 10
 *     0    0  0  1  0         0    0  1  0  1
 *     1    0  1  1  1         1    1  0  1  0
 */
