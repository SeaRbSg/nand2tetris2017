// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/RAM64.hdl

/**
 * Memory of 64 registers, each 16 bit-wide. Out hold the value
 * stored at the memory location specified by address. If load=1, then
 * the in value is loaded into the memory location specified by address
 * (the loaded value will be emitted to out after the next time step.)
 */

CHIP RAM64 {
    IN in[16], load, address[6];
    OUT out[16];

    PARTS:
    DMux8Way(in=load,
             sel=address[3..5],
             a=group0,
             b=group1,
             c=group2,
             d=group3,
             e=group4,
             f=group5,
             g=group6,
             h=group7);

    RAM8(in=in, load=group0, address=address[0..2], out=out0);
    RAM8(in=in, load=group1, address=address[0..2], out=out1);
    RAM8(in=in, load=group2, address=address[0..2], out=out2);
    RAM8(in=in, load=group3, address=address[0..2], out=out3);
    RAM8(in=in, load=group4, address=address[0..2], out=out4);
    RAM8(in=in, load=group5, address=address[0..2], out=out5);
    RAM8(in=in, load=group6, address=address[0..2], out=out6);
    RAM8(in=in, load=group7, address=address[0..2], out=out7);

    Mux8Way16(a=out0,
              b=out1,
              c=out2,
              d=out3,
              e=out4,
              f=out5,
              g=out6,
              h=out7,
              sel=address[3..5],
              out=out);
}